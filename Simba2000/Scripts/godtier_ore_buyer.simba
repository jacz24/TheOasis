{$I WaspLib/osrs.simba}



type
  EOreBuyerState = enum(
    LOGIN,

    OPEN_BANK,
    DEPOSIT_ORES,
    CLOSE_BANK,

    OPEN_SHOP,
    BUY_ORES,
    CLOSE_SHOP,

    HOP_WORLD,

    DO_ANTIBAN,
    WAIT_STATE,

    NO_ACTIVITY, MAX_ACTIONS, MAX_TIME, END_SCRIPT
  );

  TShopItem = record
    Ore: TRSItem;
    Count: Integer;
  end;

  TBuyRequest = record
    Item: TRSItem;
    InStockLimit: Integer;
    TotalLimit: Integer;
    BoughtCount: Integer;
  end;

  TOreBuyer = record
    Actions, MaxTime, MaxActions: UInt64;
    FormSetting: Boolean;

    BuyRequests: array of TBuyRequest;
    OresToBuy: TRSItemArray;
    ShopItems: array of TShopItem;
    OrdanNPC: TRSEntity;
    BankChest, Ordan: TRSObject;
  end;

function TOreBuyer.GetReportValues(): TStringArray;
var
  actionsStr, sleepInfo: String;
begin
  actionsStr := ToStr(Self.Actions);
  if Self.MaxActions <> 0 then
    actionsStr += '/' + ToStr(Self.MaxActions);

  if Antiban.Sleeps = [] then
    sleepInfo := 'No sleep'
  else
    sleepInfo := Antiban.TimeUntilSleep(Antiban.Sleeps.First, TIME_SHORT);

  Result := [
    GetTimeStamp(TIME_SHORT),
    Logger.TimeRunning.ElapsedFmt(TIME_SHORT),
    Antiban.TimeRunning.ElapsedFmt(TIME_SHORT),
    sleepInfo,
    actionsStr
  ];
end;

procedure TOreBuyer.SetupShop();
var
  tempShopItem: TShopItem;
begin
  Self.ShopItems.Clear;
  tempShopItem.Ore := 'Copper ore';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;

  tempShopItem.Ore := 'Tin ore';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;

  tempShopItem.Ore := 'Iron ore';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;

  tempShopItem.Ore := 'Mithril ore';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;

  tempShopItem.Ore := 'Silver ore';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;

  tempShopItem.Ore := 'Gold ore';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;

  tempShopItem.Ore := 'Coal';
  tempShopItem.Count := 100;
  Self.ShopItems += tempShopItem;
end;

procedure TOreBuyer.SetupBuyRequests();
var
  i: Integer;
  tempBuyRequest: TBuyRequest;
begin
  // This will eventually work through a UI Setup but for now
  // we will manually build a list
  tempBuyRequest.Item := 'Coal';
  tempBuyRequest.InStockLimit := 32;
  tempBuyRequest.TotalLimit := -1;
  Self.BuyRequests += tempBuyRequest;


  tempBuyRequest.Item := 'Gold ore';
  tempBuyRequest.InStockLimit := 98;
  tempBuyRequest.TotalLimit := -1;
  Self.BuyRequests += tempBuyRequest;

  for i := 0 to High(Self.BuyRequests) do
    Self.OresToBuy += Self.BuyRequests[i].Item;

end;


procedure TOreBuyer.Init();
begin
  Logger.Setup('GodTier Ore Buyer');
  ProgressReport.Setup(
    'GodTier Ore Buyer',
    [
      'Script runtime:', 'Botting runtime:', 'Antiban runtime:', 'Next sleep:',
      'Actions:'
    ],
    @Self.GetReportValues
  );

  Map.Setup([ERSChunk.BLAST_FURNACE]);
  Antiban.Zoom.Min := 0;
  Antiban.Zoom.Max := 50;
  Antiban.Skills := [ERSSkill.TOTAL];

  Self.SetupShop();
  Self.SetupBuyRequests();

  Self.OrdanNPC := TRSEntity.Create(@Map.Walker, [0.5, 0.5,0.5], 2, [[3648, 30566]], ['Ordan'], [ERSMinimapDot.NPC]);
  Self.OrdanNPC.Finder.Colors += [$7A93AA, 21.021, EColorSpace.RGB, [1.150, 1.060, 0.792]];
  Self.OrdanNPC.Finder.Colors += [$597782, 10.848, EColorSpace.HSV, [1.446, 0.876, 0.680]];

  Self.Ordan := TRSObject.Create(@Map.Walker, [0.5, 0.5, 3], [[3648, 30566]], ['Ordan']);
  Self.Ordan.Finder.Colors += [$7A93AA, 21.021, EColorSpace.RGB, [1.150, 1.060, 0.792]];
  Self.Ordan.Finder.Colors += [$597782, 10.848, EColorSpace.HSV, [1.446, 0.876, 0.680]];

  Self.BankChest := TRSObject.Create(@Map.Walker, [1, 1, 2], [[3696, 30606]], ['Bank chest']);
  //Self.Ordan := TRSEntity.Create(NPCsJSON.GetByName('Ordan', 1).Item[0]);
  //Self.BankChest := TRSObject.Create(ObjectsJSON.GetByID(26707));
  //Shop.Open(Self.Ordan);
  //Writeln(Map.Position());
  //ShowOnTarget(Self.Ordan);
  //TerminateScript('Test');

end;

function TOreBuyer.BuyLimitsReached(): Boolean;
var
  i: Integer;
begin
  Result := True;

  for i := 0 to High(Self.BuyRequests) do
  begin
    // -1 means unlimited, so limit not reached
    if Self.BuyRequests[i].TotalLimit = -1 then
      Exit(False);

    // If any request hasn't hit its limit, we're not done
    if Self.BuyRequests[i].BoughtCount < Self.BuyRequests[i].TotalLimit then
      Exit(False);
  end;
end;

function TOreBuyer.IsOresBuyable(): Boolean;
var
  i, j: Integer;
begin
  Result := False;

  for i := 0 to High(Self.BuyRequests) do
  begin

    // Check if we've already hit our total limit for this request
    if (Self.BuyRequests[i].TotalLimit <> -1) and
       (Self.BuyRequests[i].BoughtCount >= Self.BuyRequests[i].TotalLimit) then
      Continue;

    // Find matching shop item
    for j := 0 to High(Self.ShopItems) do
    begin
      if Self.ShopItems[j].Ore = Self.BuyRequests[i].Item then
      begin
        // Check if shop stock meets our minimum threshold
        if Self.ShopItems[j].Count >= Self.BuyRequests[i].InStockLimit then
          Exit(True);

        Break; // Found matching ore, no need to check other shop items
      end;
    end;
  end;
end;

function TOreBuyer.GetBuyableOre(): TRSItem;
var
  i, j: Integer;
begin
  Result := '';

  for i := 0 to High(Self.BuyRequests) do
  begin
    if (Self.BuyRequests[i].TotalLimit <> -1) and
       (Self.BuyRequests[i].BoughtCount >= Self.BuyRequests[i].TotalLimit) then
      Continue;

    for j := 0 to High(Self.ShopItems) do
    begin
      if Self.ShopItems[j].Ore = Self.BuyRequests[i].Item then
      begin
        if Self.ShopItems[j].Count >= Self.BuyRequests[i].InStockLimit then
          Exit(Self.BuyRequests[i].Item);
        Break;
      end;
    end;
  end;
end;

procedure TOreBuyer.OpenBank();
begin
  Writeln('Openning Bank!');
  Self.BankChest.WalkInteract(['Use']);

  SleepUntil(Bank.IsOpen(), 50, 2000);
end;

procedure TOreBuyer.DepositOres();
begin
  Writeln('Depositing Ores!');
  Bank.DepositInventory();

  SleepUntil(Inventory.Items.ContainsAny(Self.OresToBuy), 50, 1000);
end;

procedure TOreBuyer.OpenShop();
begin
  Writeln('Openning Shop!');
  Self.Ordan.WalkInteract(['Trade']);

  SleepUntil(Shop.IsOpen(), 50, 2000);
  //Shop.Open(Self.Ordan, True);
end;

procedure TOreBuyer.HopWorld();
begin
  Writeln('Hopping Worlds!');

  SleepUntil(WorldSwitcher.Next(), 500, 5000);
  Self.SetupShop();
end;

procedure TOreBuyer.UpdateOreShopCache();
var
  i: Integer;
begin
  if Shop.IsOpen() then
  begin
    for i := 0 to High(Self.ShopItems) do
    begin
      if Shop.Items.Contains(Self.ShopItems[i].Ore) then
      begin
        Self.ShopItems[i].Count := Shop.Items.ReadStack(Self.ShopItems[i].Ore);
      end;
    end;
  end;
end;

procedure TOreBuyer.BuyOres();
var
  ore: TRSItem;
  inventorycount: Integer;
begin
  Writeln('Buying Ores!');
  Self.UpdateOreShopCache();
  ore := Self.GetBuyableOre();
  inventorycount := Inventory.Items.Count(ore);
  Shop.Items.Interact(ore, 'Buy 50');
  Inventory.Items.WaitCount(ore, inventorycount + 1, 50, 1000);
  Self.UpdateOreShopCache();
end;

function TOreBuyer.GetState(): EOreBuyerState;
begin
  // Misc States
  if not RSClient.IsLoggedIn() then
    Exit(EOreBuyerState.LOGIN);

  if Self.BuyLimitsReached then
    Exit(EOreBuyerState.END_SCRIPT);

  if InRange(Inventory.Items.ReadStack('Coins'), 1, 8000) or not Inventory.Items.Contains('Coins') then
  begin
    if Inventory.Items.ReadStack('Coins') = -1 then
      Logger.Info('No Coins in Inventory!');
    Logger.Info('Coin Stack Low: ' + ToStr(Inventory.Items.ReadStack('Coins')));
    Exit(EOreBuyerState.END_SCRIPT);
  end;


  // Wasp States
  if Activity.IsFinished then
    Exit(EOreBuyerState.NO_ACTIVITY);

  if (Self.MaxActions > 0) and (Self.Actions >= Self.MaxActions) then
    Exit(EOreBuyerState.MAX_ACTIONS);

  if (Self.MaxTime > 0) and (Logger.TimeRunning.Elapsed >= Self.MaxTime) then
    Exit(EOreBuyerState.MAX_TIME);


  // Bank States
  if Inventory.IsFull() and not Shop.IsOpen() and not Bank.IsOpen() then
    Exit(EOreBuyerState.OPEN_BANK);

  if Bank.IsOpen() and Inventory.IsFull() then
    Exit(EOreBuyerState.DEPOSIT_ORES);

  if Bank.IsOpen() and not Inventory.IsFull() and not Inventory.Items.ContainsAny(Self.OresToBuy) then
    Exit(EOreBuyerState.CLOSE_BANK);




  // Shop States
  if not Inventory.IsFull() and not Shop.IsOpen() and not Bank.IsOpen() and Self.IsOresBuyable() then
    Exit(EOreBuyerState.OPEN_SHOP);

  if Shop.IsOpen() then
  begin
    Self.UpdateOreShopCache(); // Update Ore shop cache before making choice

    if not Inventory.IsFull() and Self.IsOresBuyable() then
      Exit(EOreBuyerState.BUY_ORES);

    if Inventory.IsFull() or not Self.IsOresBuyable() then
      Exit(EOreBuyerState.CLOSE_SHOP);
  end;

  // World hopping
  if not Self.IsOresBuyable() and not Shop.IsOpen() and not Bank.IsOpen() then
    Exit(EOreBuyerState.HOP_WORLD);

  Result := EOreBuyerState.WAIT_STATE;
end;

procedure TOreBuyer.Run();
var
  state: EOreBuyerState;
begin
  Self.Init();

  repeat
    state := Self.GetState();
    Logger.Info(ToStr(state));
    ProgressReport.Print();
    WaspClient.SubmitStats();

    case state of
      EOreBuyerState.LOGIN: Login.DoLogin();
      EOreBuyerState.OPEN_BANK: Self.OpenBank();
      EOreBuyerState.DEPOSIT_ORES: Self.DepositOres();
      EOreBuyerState.CLOSE_BANK: Bank.Close();

      EOreBuyerState.OPEN_SHOP: Self.OpenShop();
      EOreBuyerState.BUY_ORES: Self.BuyOres();
      EOreBuyerState.CLOSE_SHOP: Shop.Close();
      EOreBuyerState.HOP_WORLD: Self.HopWorld();
      EOreBuyerState.WAIT_STATE: Sleep(100, 200);

      EOreBuyerState.NO_ACTIVITY, EOreBuyerState.MAX_ACTIONS,
      EOreBuyerState.MAX_TIME, EOreBuyerState.END_SCRIPT: Break;
    end;

    //Antiban.DoAntiban();
  until False;

  Logger.Info('Thank you for using ' + Logger.Name);
end;

var
  OreBuyer: TOreBuyer;
  TemplateForm: TScriptForm;
  TemplateConfig: TConfigJSON;
  FormCheckbox: TLazCheckBox;

procedure TScriptForm.OnStart(sender: TLazObject); override;
begin
  inherited;

  OreBuyer.FormSetting := FormCheckbox.IsChecked();
  if TemplateConfig.Data.Has('form_setting') then
    TemplateConfig.Data.Item['form_setting'].AsBool := OreBuyer.FormSetting
  else
    TemplateConfig.Data.AddBool('form_setting', OreBuyer.FormSetting);

  OreBuyer.MaxActions := Self.Goals.Actions.Value;
  OreBuyer.MaxTime    := Self.Goals.Time.Value * ONE_MINUTE;

  TemplateConfig.Save();
end;

procedure TScriptForm.Init();
var
  tab: TLazTabSheet;
  panel: TLazPanel;
begin
  TemplateConfig.Setup('scripts' + PATH_SEP + 'godtier-ore-buyer');

  Self.Setup('GodTier Ore Buyer', TemplateConfig.Data);

  tab := Self.CreateTab('GodTier Ore Buyer');
  panel := Self.CreateGoals(tab, True, True, False, EOrientation.HORIZONTAL);
  panel.Align := ELazAlign.Bottom;

  FormCheckbox := TLazCheckBox.CreateEx(tab, 'My Setting', 'My setting does cool stuff', 20, 220);
  if TemplateConfig.Data.Has('form_setting') then
    FormCheckbox.SetChecked(TemplateConfig.Data.Item['form_setting'].AsBool)
  else
    FormCheckbox.SetChecked(True);

  Self.CreateAntibanTab();
  Self.Run();
end;

begin
  TemplateForm.Init();

  OreBuyer.Run();
end.
